# Set variables:
#   - TEST_DIR = Directory where tests are
#   - CMAKE_TEST_DIR = Directory where is the script used to find differences
SET(TEST_DIR ${CMAKE_SOURCE_DIR}/tests)
SET(CMAKE_TEST_DIR ${CMAKE_SOURCE_DIR}/cmake/tests)

MACRO(NEW_TEST TEST_SRC)

  GET_FILENAME_COMPONENT( FILE_NAME ${TEST_SRC} NAME_WE)

  GET_FILENAME_COMPONENT( DIR_PATH ${TEST_SRC} DIRECTORY)
  GET_FILENAME_COMPONENT( DIR_NAME ${DIR_PATH} NAME)

  GET_FILENAME_COMPONENT( RUN_DIR_NAME ${DIR_PATH} ABSOLUTE)
  STRING(REGEX REPLACE "${CMAKE_SOURCE_DIR}/tests/" "" RUN_DIR_NAME ${RUN_DIR_NAME} )

  STRING(REPLACE "${CMAKE_SOURCE_DIR}/tests/" "" DIR_NAME ${DIR_PATH})
  SET(WORKING_TEST_DIR "${CMAKE_BINARY_DIR}/tests/${DIR_NAME}/${FILE_NAME}")

  FILE(MAKE_DIRECTORY ${WORKING_TEST_DIR})
  ADD_EXECUTABLE(${FILE_NAME} ${TEST_SRC})
  TARGET_LINK_LIBRARIES(${FILE_NAME} ${_project_lib})
  SET_TARGET_PROPERTIES(${FILE_NAME}
    PROPERTIES RUNTIME_OUTPUT_DIRECTORY ${WORKING_TEST_DIR})

  ADD_TEST(NAME ${FILE_NAME}
    WORKING_DIRECTORY  ${WORKING_TEST_DIR}
    COMMAND ${CMAKE_COMMAND}
    -DTEST_DIR=${WORKING_TEST_DIR}
    -DTEST_NAME=${DIR_PATH}/${FILE_NAME}
    -DTESTS_DIR=${WORKING_TEST_DIR}
    -DTEST_PROG=$<TARGET_FILE:${FILE_NAME}>
    -P ${CMAKE_TEST_DIR}/runtest.cmake)

  STRING(REGEX REPLACE "/" "-" _run_target_name "run_${RUN_DIR_NAME}-${FILE_NAME}" )
  ADD_CUSTOM_TARGET(${_run_target_name}
    COMMAND ${WORKING_TEST_DIR}/${FILE_NAME}
    DEPENDS ${WORKING_TEST_DIR}/${FILE_NAME}
    WORKING_DIRECTORY ${WORKING_TEST_DIR}
  )

  STRING(REGEX REPLACE "/" "-" _check_diff_target_name "check_diff_${RUN_DIR_NAME}-${FILE_NAME}" )
  ADD_CUSTOM_TARGET(${_check_diff_target_name}
    COMMAND sh ${CMAKE_SOURCE_DIR}/cmake/scripts/show_diff.sh
                ${DIR_PATH}/${FILE_NAME}.output
                ${WORKING_TEST_DIR}/output
    DEPENDS ${WORKING_TEST_DIR}/${FILE_NAME}
    WORKING_DIRECTORY ${WORKING_TEST_DIR}
  )

  # TODO: add check differences

ENDMACRO()
